(ns virtual-keyboard.keyboard-key
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/gestures.dart" :as gesture]
   ["package:mongol/mongol.dart" :as mgl]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [virtual-keyboard.utils :as utils]
   [virtual-keyboard.keyboard-overlay :as keyboard-overlay]
   [virtual-keyboard.keyboard-state :as state]
   [cljd.flutter :as f]))


(defn key-widget [virtual-key _items-count rows-count custom-fns]
  (f/widget
   :context ctx
   :get {:value-of [:info]}
   (m/Expanded)
   (m/Padding
    .padding
    (.symmetric
     m/EdgeInsets
     .horizontal
     (:keyboard/horizontal-key-padding info)
     .vertical 0))
   (m/Container
    .clipBehavior (.hardEdge m/Clip)
    .height (/ (:keyboard/height info) rows-count)
    .decoration (m/BoxDecoration
                 .borderRadius
                 (.all m/BorderRadius
                       (.circular m/Radius (:keyboard/key-cap-border-radius info)))))
   (m/Material .color (:keyboard/key-container-color info))
   (m/GestureDetector
    .behavior m.HitTestBehavior/opaque
    .onLongPressStart
    (fn [^gesture/LongPressStartDetails details]
      (keyboard-overlay/remove-overlay-bubble)
      (keyboard-overlay/insert-overlay-popup ctx info virtual-key)
      (keyboard-overlay/set-drag-start-x (.-globalPosition details))
      (keyboard-overlay/set-drag-dx 0))
    .onLongPressMoveUpdate
    (fn [^gesture/LongPressMoveUpdateDetails details]
      (let [current-position (.-globalPosition details)
            start-position (keyboard-overlay/get-drag-start-x)]
        (when start-position
          (let [dx (- (.-dx current-position) (.-dx start-position))]
            (keyboard-overlay/set-drag-dx dx)
            (keyboard-overlay/update-overlay-selection)))))
    .onLongPressEnd
    (fn [^gesture/LongPressEndDetails _]
      (let [selected-key (keyboard-overlay/get-selected-key)]
        (when selected-key
          (keyboard-action/on-key-press
           (assoc selected-key :key-type :string) nil))
        (keyboard-overlay/remove-overlay-popup)
        (keyboard-overlay/clear-drag-state)))
    .onLongPressCancel
    (fn []
      (keyboard-overlay/remove-overlay-popup)
      (keyboard-overlay/clear-drag-state)))
   (m/InkWell
    .splashColor (-> m.Colors/black (.withOpacity 0.1))
    .highlightColor (-> m.Colors/grey .-shade200 (.withOpacity 0.3))
    .onTap
    (fn []
      (keyboard-overlay/remove-overlay-bubble)
      (keyboard-action/on-key-press virtual-key custom-fns))
    .onTapDown (fn [_]
                 (keyboard-overlay/remove-overlay-popup)
                 (keyboard-overlay/insert-overlay-bubble ctx info virtual-key))
    .onTapUp (fn [_]
               (keyboard-overlay/remove-overlay-bubble))
    .onTapCancel (fn []
                   (keyboard-overlay/remove-overlay-bubble))
    .enableFeedback false)
   (m/Stack
    .children
    [(if (and (zero? (:keyboard/layout-index @state/state))
              (not (some #{(:id virtual-key)} ["ng" "-" "_"]))
              (not-empty (:id virtual-key)))
       (m/Align .alignment m.Alignment/topLeft
                .child (m/Text (:id virtual-key)
                               .style (m/TextStyle .color (:keyboard/key-text-color info))))
       (m/SizedBox))
     (m/Align .alignment (.center m/Alignment)
              .child
              (if (:is-rotated virtual-key)
                (mgl/MongolText (utils/key-text virtual-key)
                                .style
                                (m/TextStyle .fontSize (:keyboard/font-size info)
                                             .color (:keyboard/key-text-color info)
                                             .fontFamily (:keyboard/mongol-font-family info)
                                             .fontWeight (:keyboard/font-weight info)))
                (m/Text (utils/key-text virtual-key)
                        .style
                        (m/TextStyle .fontSize (:keyboard/font-size info)
                                     .color (:keyboard/key-text-color info)
                                     .fontFamily (:keyboard/english-font-family info)
                                     .fontWeight (:keyboard/font-weight info)))))])))
