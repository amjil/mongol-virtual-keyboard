(ns virtual-keyboard.utils
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/services.dart" :as services]
   ["package:mongol_code/mongol_code.dart" :as code]
   ["package:mongol/mongol.dart" :as mgl]
   [clojure.string :as str]))

(defn get-key-button-size [ctx]
  (let [screen-width (-> m/MediaQuery (.of ctx) .-size .-width)
        screen-height (-> m/MediaQuery (.of ctx) .-size .-height)
        width (* screen-width (if (> screen-width 350) 0.084 0.082))
        height (* screen-height (if (> screen-height 800) 0.059 0.07))]
    [width height]))

(defn key-text [state vkey]
  (cond 
    (some? (:display vkey))
    (:display vkey)
    
    (and (:keyboard/is-shift-enabled @state)
         (some? (:caps-text vkey)))
    (:caps-text vkey)
    
    :else 
    (:text vkey)))

(defn get-last-word [text]
  (if (empty? text)
    nil
    (-> text
        (str/split #" ")
        last)))

(defn get-current-word [text ^services/TextSelection selection]
  (if (empty? text)
    ""
    (let [back-count 30
          substr (subs text (max 0 (- (.-start selection) back-count)) (.-end selection))
          last-word (str/trim (get-last-word substr))]
      (if (empty? last-word)
        ""
        last-word))))