(ns virtual-keyboard.keyboard-overlay
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/services.dart" :as service]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [virtual-keyboard.popup-key :as popup-key]
   [virtual-keyboard.utils :as utils]
   [virtual-keyboard.popup-key-candidates :as candidates]
   [cljd.flutter :as f]
   [virtual-keyboard.input-control :as control]))

(defn bubble-widget [^m/Size size info state virtual-key]
  (f/widget
   (m/Material
    .elevation 10
    .color (:keyboard/key-container-color info))
   (m/Container
    .clipBehavior (.hardEdge m/Clip)
    ;; .width (* 1.5 (.-width size))
    ;; .height (* 1.5 (.-height size))
    .width (* 1 (.-width size))
    .height (* 1 (.-height size))
    .alignment m.Alignment/center
    .decoration (m/BoxDecoration
                 .borderRadius
                 (.all m/BorderRadius
                       (.circular m/Radius (:keyboard/key-cap-border-radius info)))))
   (m/RotatedBox
    .quarterTurns (if (:is-rotated virtual-key) 1 0)
    .child (m/Text (utils/key-text state virtual-key)
                   .style
                   (m/TextStyle .fontSize 24
                                .color (:keyboard/key-text-color info)
                                .fontFamily "MongolianBaiZheng")))))

(defn build-entry-bubble [^m/BuildContext ctx
                          info
                          state
                          virtual-key]
  (let [^m/RenderBox box (.findRenderObject ctx)
        size (.-size box)
        offset (.localToGlobal box (.zero m/Offset))]
    (m/OverlayEntry
     .builder (fn [_]
                (m/Positioned
                 .top (- (.-dy offset) (* 1 (.-height size)))
                 .left (+ (.-dx offset) (/ (.-width size) 2) (- (/ (* 1 (.-width size)) 2)))
                 .child
                 (bubble-widget size info state virtual-key))))))

(defn remove-overlay
  [state]
  (when-not (nil? (:keyboard/entry @state))
    (let [^m/OverlayEntry entry (:keyboard/entry @state)]
      (when-not (nil? entry)
        (.remove entry))
      (swap! state assoc :keyboard/entry nil))))

(defn insert-overlay-bubble
  [state
   ^m/BuildContext ctx
   info
   current-key]
  (remove-overlay state)
  (let [^m/OverlayEntry entry (build-entry-bubble ctx info state current-key)]
    (swap! state assoc :keyboard/entry nil)
    (.insert (.of m/Overlay ctx) entry)
    (swap! state assoc :keyboard/entry entry)))


;;;;
(defn overlay-popup-widget [^m/Size size info state additional-keys is-rotated]
  (f/widget
   :context ctx
   (m/GestureDetector)
   (m/Card
    .elevation 10)
   (m/Container
    .color (-> m/Theme (.of ctx) .-colorScheme .-primaryContainer))
   (m/Wrap
    .direction m.Axis/horizontal
    .runSpacing 2
    .spacing 1
    .children
    (map (fn [vkey]
           (m/Card
            .child
            (m/ConstrainedBox
             .constraints (m/BoxConstraints
                           .maxHeight (/ (:keyboard/height info) 4))
             .child
             (m/DragTarget
              .builder (fn [_ _ _]
                         (popup-key/key-widget info state vkey is-rotated))
              .onAccept (fn [_]
                          (keyboard-action/on-key-press
                           (assoc vkey :key-type :string) state))))))
         additional-keys))
   ))


(defn build-entry-popup [^m/BuildContext ctx
                         info
                         state
                         additional-keys
                         is-rotated]
  (let [^m/RenderBox box (.findRenderObject ctx)
        size (.-size box)
        offset (.localToGlobal box (.zero m/Offset))
        screen-width (-> m/MediaQuery (.of ctx) .-size .-width)]
    (m/OverlayEntry
     .builder (fn [_]
                (m/Positioned
                 .top (- (.-dy offset) (+ 10 (.-height size)))
                 .right (if (>= (.-dx offset) (/ screen-width 2))
                          (- screen-width (.-dx offset) (.-width size))
                          nil)
                 .left (if (< (.-dx offset) (/ screen-width 2))
                         (.-dx offset)
                         nil)
                 .child
                 (overlay-popup-widget size info state additional-keys is-rotated))))))


(defn insert-overlay-popup
  [state
   ^m/BuildContext ctx
   info
   virtual-key]
  (let [^service/TextEditingValue editing-value @control/editing-value
        additional-keys
        (if (true? (:is-rotated virtual-key))
          (candidates/popup-candidates editing-value (:id virtual-key))
          (get popup-key/en-popup-keys (keyword (:text virtual-key))))]
    (if (zero? (count additional-keys))
      nil
      (do
        (remove-overlay state)
        (let [^m/OverlayEntry entry (build-entry-popup ctx info state additional-keys (:is-rotated virtual-key))]
          (swap! state assoc :keyboard/entry nil)
          (.insert (.of m/Overlay ctx) entry)
          (swap! state assoc :keyboard/entry entry))))))