(ns virtual-keyboard.keyboard-overlay
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:flutter/services.dart" :as service]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [virtual-keyboard.keyboard-action :as keyboard-action]
   [virtual-keyboard.popup-key :as popup-key]
   [virtual-keyboard.utils :as utils]
   [virtual-keyboard.popup-key-candidates :as candidates]
   [virtual-keyboard.keyboard-state :as state]
   [virtual-keyboard.input-control :as control]))

(defn bubble-widget [^m/Size size info virtual-key]
  (f/widget
   (m/Material
    .elevation 10
    .color (:keyboard/key-container-color info))
   (m/Container
    .clipBehavior (.hardEdge m/Clip)
    ;; .width (* 1.5 (.-width size))
    ;; .height (* 1.5 (.-height size))
    .width (* 1 (.-width size))
    .height (* 1 (.-height size))
    .alignment m.Alignment/center
    .decoration (m/BoxDecoration
                 .borderRadius
                 (.all m/BorderRadius
                       (.circular m/Radius (:keyboard/key-cap-border-radius info)))))
   (if (:is-rotated virtual-key)
     (mgl/MongolText (utils/key-text virtual-key)
                     .style (m/TextStyle .fontSize 18
                                         .color (:keyboard/key-text-color info)
                                         .fontFamily (:keyboard/mongol-font-family info)))
     (m/Text (utils/key-text virtual-key)
             .style (m/TextStyle .fontSize 18
                                 .color (:keyboard/key-text-color info))))))

(defn build-entry-bubble [^m/BuildContext ctx
                          info
                          virtual-key]
  (let [^m/RenderBox box (.findRenderObject ctx)
        size (.-size box)
        offset (.localToGlobal box (.zero m/Offset))]
    (m/OverlayEntry
     .builder (fn [_]
                (m/Positioned
                 .top (- (.-dy offset) (* 1 (.-height size)))
                 .left (+ (.-dx offset) (/ (.-width size) 2) (- (/ (* 1 (.-width size)) 2)))
                 .child
                 (bubble-widget size info virtual-key))))))

(defn remove-overlay
  []
  (when-not (nil? (:keyboard/entry @state/state))
    (let [^m/OverlayEntry entry (:keyboard/entry @state/state)]
      (when-not (nil? entry)
        (.remove entry))
      (swap! state/state assoc :keyboard/entry nil))))

(defn insert-overlay-bubble
  [^m/BuildContext ctx
   info
   current-key]
  (remove-overlay)
  (let [^m/OverlayEntry entry (build-entry-bubble ctx info current-key)]
    (swap! state/state assoc :keyboard/entry nil)
    (.insert (.of m/Overlay ctx) entry)
    (swap! state/state assoc :keyboard/entry entry)))


;;;;
(defn overlay-popup-widget [^m/Size size info additional-keys is-rotated]
  (f/widget
   :context ctx
   (m/GestureDetector)
   (m/Card
    .elevation 10)
   (m/Container
    .color (-> m/Theme (.of ctx) .-colorScheme .-primaryContainer))
   (m/Wrap
    .direction m.Axis/horizontal
    .runSpacing 2
    .spacing 1
    .children
    (map (fn [vkey]
           (m/Card
            .child
            (m/ConstrainedBox
             .constraints (m/BoxConstraints
                           .maxHeight (/ (:keyboard/height info) 4))
             .child
             (m/DragTarget
              .builder (fn [_ _ _]
                         (popup-key/key-widget info vkey is-rotated))
              .onAccept (fn [_]
                          (keyboard-action/on-key-press
                           (assoc vkey :key-type :string) nil))))))
         additional-keys))
   ))


(defn build-entry-popup [^m/BuildContext ctx
                         info
                         additional-keys
                         is-rotated]
  (let [^m/RenderBox box (.findRenderObject ctx)
        size (.-size box)
        offset (.localToGlobal box (.zero m/Offset))
        screen-width (-> m/MediaQuery (.of ctx) .-size .-width)]
    (m/OverlayEntry
     .builder (fn [_]
                (m/Positioned
                 .top (- (.-dy offset) (+ 10 (.-height size)))
                 .right (if (>= (.-dx offset) (/ screen-width 2))
                          (- screen-width (.-dx offset) (.-width size))
                          nil)
                 .left (if (< (.-dx offset) (/ screen-width 2))
                         (.-dx offset)
                         nil)
                 .child
                 (overlay-popup-widget size info additional-keys is-rotated))))))


(defn insert-overlay-popup
  [^m/BuildContext ctx
   info
   virtual-key]
  (let [^service/TextEditingValue editing-value @control/editing-value
        additional-keys
        (if (true? (:is-rotated virtual-key))
          (candidates/popup-candidates editing-value (:id virtual-key))
          (get popup-key/en-popup-keys (keyword (:text virtual-key))))]
    (if (zero? (count additional-keys))
      nil
      (do
        (remove-overlay)
        (let [^m/OverlayEntry entry (build-entry-popup ctx info additional-keys (:is-rotated virtual-key))]
          (swap! state/state assoc :keyboard/entry nil)
          (.insert (.of m/Overlay ctx) entry)
          (swap! state/state assoc :keyboard/entry entry))))))